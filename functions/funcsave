##? funcsave - save a function to $ZFUNCDIR for lazy loading

#function funcsave {

local ZFUNCDIR="${ZFUNCDIR:-${ZDOTDIR:-$HOME/.config/zsh}/functions}"

# check args
if [[ -z "$1" ]]; then
  echo >&2 "funcsave: Expected function name argument"
  return 1
elif ! typeset -f "$1" > /dev/null; then
  echo >&2 "funcsave: Unknown function '$1'"
  return 1
elif [[ ! -d "$ZFUNCDIR" ]]; then
  echo >&2 "funced: Directory not found '$ZFUNCDIR'."
  return 1
fi

# make sure the function is loaded in case it's already lazy
autoload +X "$1" > /dev/null

# remove first/last lines (ie: 'function foo {' and '}') and de-indent one level
type -f "$1" | awk 'NR>2 {print prev} {gsub(/^\t/, "", $0); prev=$0}' >| "$ZFUNCDIR/$1"

#}

# vim: ft=zsh sw=2 ts=2 et
